/*!
 *  Bayrell Core Library
 *
 *  (c) Copyright 2018-2019 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
 
namespace Core.Components.Navigator;

use Runtime.RuntimeUtils;
use Core.Components.ContextMenu.ContextMenuItemClickEvent;
use Core.Components.Navigator.NavigatorItem;
use Core.Components.Navigator.NavigatorItemEvent;
use Core.Components.Tree.TreeItem;
use Core.Components.Tree.TreeItemEvent;
use Core.Components.Tree.TreeManager;
use Core.Components.Tree.TreeModel;
use Core.FileSystem.FileNode;
use Core.Http.ApiRequest;
use Core.UI.UIController;
use Core.UI.Annotations.BindValue;
use Core.UI.Annotations.BindModel;
use Core.UI.Annotations.Event;
use Core.UI.Annotations.EventAsync;
use Core.UI.Events.ModelChange;
use Core.UI.Events.MouseEvent.MouseClickEvent;
use Core.UI.Events.MouseEvent.MouseDoubleClickEvent;
use Core.UI.Events.MouseEvent.MouseContextMenuEvent;
use Core.UI.Events.UserEvent.ChangeEvent;
use Core.UI.Render.CoreManager;


class NavigatorManager extends TreeManager
{
	public NavigatorModel model;
	
	
	/**
	 * Create new item
	 */
	public TreeItem newItem(Dict map)
	{
		return new NavigatorItem(map);
	}
	
	
	
	
	/* ---------------------------------- Open Item ---------------------------------- */
	
	
	/**
	 * Open item
	 */
	void openItem(string tree_data_path)
	{
		Collection<int> path = TreeModel::splitPath(tree_data_path);
		NavigatorItem item = TreeModel::getTreeItem(this.model, path);
		
		if (item.kind == NavigatorItem::KIND_FOLDER)
		{
			if (item.items == null)
			{
				fun f = rtl::methodAwait(this, "reloadItemAsync");
				f(tree_data_path);
			}
			else
			{
				item = item.copy({
					"is_open": not item.is_open,
					"is_reload": false,
				});
				
				/* Update model */
				this.setModel( TreeModel::setTreeItem(this.model, path, item) );
				
				/* Signal Out */
				this.signalOut( new TreeItemEvent{ "kind": TreeItemEvent::OPEN, "path": path } );
			}
		}
		else if (item.kind == NavigatorItem::KIND_FILE)
		{
			fun f = rtl::methodAwait(this, "reloadItemAsync");
			f(tree_data_path);
		}
	}
	
	
	
	/* ---------------------------------- Reload Item ---------------------------------- */
	
	
	/**
	 * Reload item
	 */
	void reloadItem(string tree_data_path)
	{
		fun f = rtl::methodAwait(this, "reloadItemAsync");
		f(tree_data_path);
	}
	
	
	
	/**
	 * Event reload node
	 */
	public async void reloadItemAsync(string tree_data_path)
	{
		ContextInterface context = this.context();
		Collection<int> path = TreeModel::splitPath(tree_data_path);
		
		/* Get item */
		NavigatorItem item = TreeModel::getTreeItem(this.model, path);
		string file_path = TreeModel::getNamePath(this.model, path);
		
		/* Reload folder */
		if (item.kind == NavigatorItem::KIND_FOLDER)
		{
			/* Set is reload */
			item <= is_reload <= true;
			this.setModel( TreeModel::setTreeItem(this.model, path, item) );
			
			/* Signal out */
			this.signalOut( 
				new NavigatorItemEvent
				{ 
					"kind": NavigatorItemEvent::RELOAD_FOLDER_START,
					"item": item,
					"path": path,
					"file_path": file_path,
				} 
			);
			
			/* Call api */
			ApiResult res = await context.callApi(
				this.model.api_class_name,
				"Core.FileSystem.FileTransferInterface",
				"getDirectoryListing",
				{
					"path": file_path,
				}
			);
			
			/* Set items */
			NavigatorItem item = TreeModel::getTreeItem(this.model, path);
			
			if (res.code == 1)
			{
				/* Update item */
				NavigatorModel model = this.model;
				NavigatorItem item = TreeModel::getTreeItem(model, path);
				item = item.copy({
					"items": NavigatorItem::convertFileNodes(res.result),
					"is_open": true,
					"is_reload": false,
				});
				
				/* Signal Out */
				this.signalOut( new TreeItemEvent{ "kind": TreeItemEvent::OPEN, "path": path, "item": item } );
			}
			else
			{
				item <= is_reload <= false;
			}
			
			/* Update model */
			this.setModel( TreeModel::setTreeItem(this.model, path, item) );
			
			/* Signal out */
			this.signalOut( 
				new NavigatorItemEvent
				{ 
					"kind": NavigatorItemEvent::RELOAD_FOLDER_END,
					"item": item,
					"path": path,
					"file_path": file_path,
					"res": res,
				} 
			);
		}
		
		
		/* Reload file */
		else if (item.kind == NavigatorItem::KIND_FILE)
		{
			/* Set is reload true */
			item <= is_reload <= true;
			this.setModel( TreeModel::setTreeItem(this.model, path, item) );
			
			int offset = 0;
			int filesize = -1;
			Vector<byte> content = new Vector();
			
			/* Signal out */
			this.signalOut( 
				new NavigatorItemEvent
				{ 
					"kind": NavigatorItemEvent::OPEN_FILE_START,
					"item": item,
					"path": path,
					"file_path": file_path,
				} 
			);
			
			/* Load file content */
			while (filesize == -1 or offset < filesize)
			{
				ApiResult res = await context.callApi(
					this.model.api_class_name,
					"Core.FileSystem.FileTransferInterface",
					"readBlock",
					{
						"path": file_path,
						"offset": offset,
					}
				);
				
				if (res.code != 1) break;
				
				offset += res.result.count;
				filesize = res.result.size;
				content.appendVector(res.result.bytes);
				
				if (res.result.eof) break;
			}
			
			/* Set is reload false */
			NavigatorItem item = TreeModel::getTreeItem(this.model, path);
			item <= is_reload <= false;
			this.setModel( TreeModel::setTreeItem(this.model, path, item) );
			
			/* Signal out */
			this.signalOut( 
				new NavigatorItemEvent
				{ 
					"kind": NavigatorItemEvent::OPEN_FILE_END,
					"item": item,
					"path": path,
					"file_path": file_path,
					"content": content.toCollection()
				} 
			);
		}
		
	}
	
	
	
	
	/* ---------------------------------- Create Item ---------------------------------- */
	
	
	/**
	 * Create item
	 */
	public void createDialogItem(DialogResultEvent result)
	{
		fun f = rtl::methodAwait(this, "createItemAsync");
		f(result);
	}
	
	
	
	/**
	 * Create item
	 */
	public async void createItemAsync(DialogResultEvent result)
	{
		Collection<int> path = result.model.path;
		TreeItem item = this.createNewItemByDialogResult(result);
		
		string file_path = result.model.file_path ~ "/" ~ result.model.input;
		string api_kind = FileNode::KIND_FILE;
		if (item.kind == NavigatorItem::KIND_FOLDER) api_kind = FileNode::KIND_FOLDER;
		
		/* Signal out */
		this.signalOut( 
			new NavigatorItemEvent
			{ 
				"kind": NavigatorItemEvent::CREATE_START,
				"item": item,
				"path": path,
				"file_path": file_path,
			} 
		);
		
		
		/* Send api */
		ApiResult res = await context.callApi(
			this.model.api_class_name,
			"Core.FileSystem.FileTransferInterface",
			"createNode",
			{
				"path": file_path,
				"kind": api_kind,
				"clear": true,
			}
		);
		
		
		/* Add item*/
		TreeModel model = TreeModel::addTreeItem
		(
			this.model,
			path,
			item,
			void (Collection<TreeItem> items, TreeItem item) => return items.pushIm(item)
		);
		this.setModel( model );
		
		/* Signal out */
		this.signalOut( new TreeItemEvent{ "kind": TreeItemEvent::CREATE, "path": path, "item": item } );
		
		/* Signal out */
		this.signalOut( 
			new NavigatorItemEvent
			{ 
				"kind": NavigatorItemEvent::CREATE_END,
				"item": item,
				"path": path,
				"file_path": file_path,
				"res": res,
			} 
		);
	}
	
	
	
	
	/* ---------------------------------- Rename Item ---------------------------------- */
	
	
	/**
	 * Rename item
	 */
	public void renameDialogItem(DialogResultEvent result)
	{
		fun f = rtl::methodAwait(this, "renameItemAsync");
		f(result);
	}
	
	
	
	/**
	 * Event rename node
	 */
	public async void renameItemAsync(DialogResultEvent result)
	{
		TreeItem item = TreeModel::getTreeItem(this.model, result.model.path);
		if (item == null)
		{
			return;
		}
		
		TreeItem item_new = item;
		TreeItem item_old = item;
		item_new <= name <= result.input;
		
		ContextInterface context = this.context();
		Collection<int> path = result.model.path;
		Collection<int> p_path = path.removeLastIm();
		string file_path = TreeModel::getNamePath(this.model, p_path);
		string path_old = file_path ~ "/" ~ item_old.name;
		string old_name = item_old.name;
		string new_name = item_new.name;
		string api_kind = FileNode::KIND_FILE;
		if (item_old.kind == NavigatorItem::KIND_FOLDER) api_kind = FileNode::KIND_FOLDER;
		
		
		/* Signal out */
		this.signalOut( 
			new NavigatorItemEvent
			{ 
				"kind": NavigatorItemEvent::RENAME_START,
				"item_old": item_old,
				"item_new": item_new,
				"path": path,
				"file_path": file_path,
				"old_name": old_name,
				"new_name": new_name,
			} 
		);
		
		
		/* Send api */
		ApiResult res = await context.callApi(
			this.model.api_class_name,
			"Core.FileSystem.FileTransferInterface",
			"renameNode",
			{
				"path": file_path,
				"kind": api_kind,
				"old_name": old_name,
				"new_name": new_name,
			}
		);
		
		
		/* Set new model */
		TreeModel model = TreeModel::setTreeItem
		(
			this.model,
			result.model.path,
			item_new
		);
		this.setModel( model );
		
		
		/* Signal out */
		this.signalOut(
			new TreeItemEvent
			{
				"kind": TreeItemEvent::RENAME,
				"path": path,
				"item_new": item_new,
				"item_old": item_old,
			}
		);
		
		
		/* Signal out */
		this.signalOut( 
			new NavigatorItemEvent
			{ 
				"kind": NavigatorItemEvent::RENAME_END,
				"item_old": item_old,
				"item_new": item_new,
				"path": path,
				"file_path": file_path,
				"old_name": old_name,
				"new_name": new_name,
				"res": res,
			} 
		);
		
	}
	
	
	
	
	/* ---------------------------------- Delete Item ---------------------------------- */
	
	
	/**
	 * Remove item
	 */
	public void removeDialogItem(DialogResultEvent result)
	{
		fun f = rtl::methodAwait(this, "deleteItemAsync");
		f(result);
	}
	
	
	
	/**
	 * Event delete node
	 */
	public async void deleteItemAsync(DialogResultEvent result)
	{
		ContextInterface context = this.context();
		TreeItem item = TreeModel::getTreeItem(this.model, result.model.path);
		Collection<int> path = result.model.path;
		Collection<int> p_path = path.removeLastIm();
		string file_path = TreeModel::getNamePath(this.model, p_path) ~ "/" ~ item.name;
		
		/* Signal out */
		this.signalOut( 
			new NavigatorItemEvent
			{ 
				"kind": NavigatorItemEvent::DELETE_START,
				"item": item,
				"path": path,
				"file_path": file_path,
			} 
		);
		
		/* Delete node */
		ApiResult res = await context.callApi(
			this.model.api_class_name,
			"Core.FileSystem.FileTransferInterface",
			"deleteNode",
			{
				"path": file_path,
			}
		);
		
		
		/* Set new model */
		TreeModel model = TreeModel::deleteTreeItem
		(
			this.model,
			result.model.path
		);
		this.setModel( model );
		
		
		/* Signal out */
		this.signalOut( new TreeItemEvent{ "kind": TreeItemEvent::DELETE, "path": path, "item": item } );
		
		
		/* Signal out */
		this.signalOut( 
			new NavigatorItemEvent
			{ 
				"kind": NavigatorItemEvent::DELETE_END,
				"item": item,
				"path": path,
				"file_path": file_path,
			} 
		);
		
	}
	
}